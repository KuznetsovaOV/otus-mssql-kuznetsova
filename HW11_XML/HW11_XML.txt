/*
1. В личном кабинете есть файл StockItems.xml.
Это данные из таблицы Warehouse.StockItems.
Преобразовать эти данные в плоскую таблицу с полями, аналогичными Warehouse.StockItems.
Поля: StockItemName, SupplierID, UnitPackageID, OuterPackageID, QuantityPerOuter, TypicalWeightPerUnit, LeadTimeDays, IsChillerStock, TaxRate, UnitPrice 

Загрузить эти данные в таблицу Warehouse.StockItems: 
существующие записи в таблице обновить, отсутствующие добавить (сопоставлять записи по полю StockItemName). 

Сделать два варианта: с помощью OPENXML и через XQuery.
*/

/*OPENXML*/

DECLARE @xmlDocument XML

SELECT @xmlDocument = BulkColumn
FROM OPENROWSET(BULK 'C:\Users\kuznetsova.ov\work\StockItems-188-1fb5df.xml', SINGLE_CLOB) as t

SELECT @xmlDocument AS [@xmlDocument]

DECLARE @docHandle INT;
EXEC sp_xml_preparedocument @docHandle OUTPUT, @xmlDocument

SELECT @docHandle AS docHandle

SELECT *
into #t
FROM OPENXML(@docHandle, N'/StockItems/Item') --путь к строкам
WITH ( 
    [StockItemName] NVARCHAR(100) '@Name',
    [SupplierID] INT  'SupplierID', -- атрибут
	[UnitPackageID] INT 'Package/UnitPackageID', -- элемент
	[OuterPackageID] INT 'Package/OuterPackageID',
	[QuantityPerOuter] INT 'Package/QuantityPerOuter',
	[TypicalWeightPerUnit] decimal(18,2) 'Package/TypicalWeightPerUnit',
	[LeadTimeDays] INT 'LeadTimeDays',
	[IsChillerStock] bit 'IsChillerStock',
	[TaxRate] decimal(18,3) 'TaxRate',
	[UnitPrice] decimal(18,2) 'UnitPrice'
	
	)


	EXEC sp_xml_removedocument @docHandle

	merge Warehouse.StockItems as Target
using #t AS Source
    on (Target.[StockItemName] = Source.[StockItemName])
when matched
    then update 
        set [QuantityPerOuter] = Source.[QuantityPerOuter]
		, [TypicalWeightPerUnit] = Source.[TypicalWeightPerUnit]
		, [LeadTimeDays] = Source.[LeadTimeDays]
		, [IsChillerStock] = Source.[IsChillerStock]
		, [TaxRate] = Source.[TaxRate]
		, [UnitPrice] = Source.[UnitPrice]
when not matched 
    then insert (
		[StockItemName]
		, [SupplierID]
		, [UnitPackageID]
		, [OuterPackageID]
		, [QuantityPerOuter]
		, [TypicalWeightPerUnit]
		, [LeadTimeDays]
		, [IsChillerStock]
		, [TaxRate]
		, [UnitPrice]
		, [LastEditedBy])
        values (Source.[StockItemName]
			, Source.[SupplierID]
			, Source.[UnitPackageID]
			, Source.[OuterPackageID]
			, Source.[QuantityPerOuter]
			, Source.[TypicalWeightPerUnit]
			, Source.[LeadTimeDays]
			, Source.[IsChillerStock]
			, Source.[TaxRate]
			, Source.[UnitPrice]
			, 3);


/*XQuery*/

declare @x xml
set @x = (
		select *
		from openrowset(bulk 'C:\Users\kuznetsova.ov\work\StockItems-188-1fb5df.xml', single_clob) as d
		)

select
	[StockItemName] = t.StockItemsXML.value('(@Name)[1]', 'nvarchar(100)')
	, [SupplierID] = t.StockItemsXML.value('(SupplierID)[1]', 'int')
	, [UnitPackageID] = t.StockItemsXML.value('(Package/UnitPackageID)[1]', 'int')
	, [OuterPackageID] = t.StockItemsXML.value('(Package/OuterPackageID)[1]', 'int')
	, [QuantityPerOuter] = t.StockItemsXML.value('(Package/QuantityPerOuter)[1]', 'int')
	, [TypicalWeightPerUnit] = t.StockItemsXML.value('(Package/TypicalWeightPerUnit)[1]', 'decimal(18,3)')
	, [LeadTimeDays] = t.StockItemsXML.value('(LeadTimeDays)[1]', 'int')
	, [IsChillerStock] = t.StockItemsXML.value('(IsChillerStock)[1]', 'bit')
	, [TaxRate] = t.StockItemsXML.value('(TaxRate)[1]', 'decimal(18,3)')
	, [UnitPrice] = t.StockItemsXML.value('(UnitPrice)[1]', 'decimal(18,2)')
	into #t1
from @x.nodes('/StockItems/Item') as t(StockItemsXML)

merge Warehouse.StockItems as Target
using #t1 AS Source
    on (Target.[StockItemName] = Source.[StockItemName])
when matched 
    then update 
        set [QuantityPerOuter] = Source.[QuantityPerOuter]
		, [TypicalWeightPerUnit] = Source.[TypicalWeightPerUnit]
		, [LeadTimeDays] = Source.[LeadTimeDays]
		, [IsChillerStock] = Source.[IsChillerStock]
		, [TaxRate] = Source.[TaxRate]
		, [UnitPrice] = Source.[UnitPrice]
when not matched 
    then insert (
		[StockItemName]
		, [SupplierID]
		, [UnitPackageID]
		, [OuterPackageID]
		, [QuantityPerOuter]
		, [TypicalWeightPerUnit]
		, [LeadTimeDays]
		, [IsChillerStock]
		, [TaxRate]
		, [UnitPrice]
		, [LastEditedBy])
        values (Source.[StockItemName]
			, Source.[SupplierID]
			, Source.[UnitPackageID]
			, Source.[OuterPackageID]
			, Source.[QuantityPerOuter]
			, Source.[TypicalWeightPerUnit]
			, Source.[LeadTimeDays]
			, Source.[IsChillerStock]
			, Source.[TaxRate]
			, Source.[UnitPrice]
			, 3);

/*2. Выгрузить данные из таблицы StockItems в такой же xml-файл, как StockItems.xml*/
Select 
	  StockItemID as [@ID]
	, StockItemName as [Name]
	, SupplierID as [Supplier/ID]
	, ColorID as [Color/ID]
	, UnitPackageID as [Package/UnitID]
	, OuterPackageID as [Package/OuterID]
	, Brand as [Brand]
	, Size as [Size]
	, LeadTimeDays as [LeadTime]
	, QuantityPerOuter as [Package/Quantity]
	, IsChillerStock as [IsChillerStock]
	, Barcode as [Barcode]
	, TaxRate as [TaxRate]
	, UnitPrice as [Pricing/Unit]
	, TypicalWeightPerUnit as [Package/Weight]
	, RecommendedRetailPrice as [Pricing/Retail]
	, TypicalWeightPerUnit as [Weight/PerUnit]
	, MarketingComments as [MarketingComments]
	, InternalComments as [InternalComments]
	, Photo as [Photo]
	, CustomFields as [CustomFields]
	, Tags as [Tags]
	, SearchDetails as [SearchDetails]
	, LastEditedBy as [LastEdited/By]
	, ValidFrom as [Valid/From]
	, ValidTo as [Valid/To]
from Warehouse.StockItems
for xml path('StockItem'), root('StockItems')

/*
3. В таблице Warehouse.StockItems в колонке CustomFields есть данные в JSON.
Написать SELECT для вывода:
- StockItemID
- StockItemName
- CountryOfManufacture (из CustomFields)
- FirstTag (из поля CustomFields, первое значение из массива Tags)
*/
Select distinct
	  t.StockItemID
	, t.StockItemName
	, t1.CountryOfManufacture
	, t1.FirstTag
from [Warehouse].[StockItems] as t
outer apply openjson(CustomFields) with (
	CountryOfManufacture nvarchar(max) '$.CountryOfManufacture'
	, FirstTag nvarchar(max) '$.Tags[0]'
) t1

/*
4. Найти в StockItems строки, где есть тэг "Vintage".
Вывести: 
- StockItemID
- StockItemName
- (опционально) все теги (из CustomFields) через запятую в одном поле

Тэги искать в поле CustomFields, а не в Tags.
Запрос написать через функции работы с JSON.
Для поиска использовать равенство, использовать LIKE запрещено.

Должно быть в таком виде:
... where ... = 'Vintage'

Так принято не будет:
... where ... Tags like '%Vintage%'
... where ... CustomFields like '%Vintage%' 
*/
Select distinct
	  t.StockItemID
	, t.StockItemName
from [Warehouse].[StockItems] as t
outer apply openjson(CustomFields, '$.Tags') t1
where t1.value = 'Vintage'


